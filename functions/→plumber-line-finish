local ic=$options[interactivecomments]
[[ $CONTEXT = (start|cont) ]] || return

# Final command
if [[ -v _ZPLUMBER_ACCEPT ]]; then
	unset _ZPLUMBER_ACCEPT
	# finish, actually accept line
	echo >&$_ZPLUMBER_CTRL_FD
	exec {_ZPLUMBER_CTRL_FD}>&-
	unset _ZPLUMBER_CTRL_FD
	_ZPLUMBER_FINISH_BUFFER=$BUFFER
	BUFFER="<$_ZPLUMBER_FILE <&$_ZPLUMBER_OUT_FD $BUFFER"
	add-zle-hook-widget line-init →plumber-line-init-finish
	# TODO: history
	
	# currently, chained sessions are not supported
	return
fi

# Test command
if [[ -v _ZPLUMBER_CTRL_FD ]]; then
	# put back $BUFFER on line-init
	_ZPLUMBER_BUFFER=$BUFFER
	_ZPLUMBER_CURSOR=$CURSOR
	add-zle-hook-widget line-init →plumber-line-init-test
	BUFFER="<$_ZPLUMBER_FILE $BUFFER"
	# TODO: history
	return
fi

# No command running
if [[ $ic = off ]]; then
	# Test for trailing '|'
	[[ ${${(z)BUFFER}[-1]} = '|' ]] || return

else
	# Test for trailing '|' after removing comments
	[[ ${${(Z[C])BUFFER}[-1]} = '|' ]] || return

	local -a reply line
	local REPLY REPLY2 nl=$'\n'
	split-shell-arguments
	local -i line_i=$reply[(I)$nl]
	line=("${(@)reply[line_i,-1]}")     # last line
	local -i comment_i=${line[(i)\#*]}  # first comment
	local new_BUFFER
	if ((comment_i)); then
		new_BUFFER=${(j::)reply[1,line_i+comment_i-1]}
		# if we have a comment, "$FIRST | #comment $'\n'$REST"
		BUFFER+=$nl
	else
		new_BUFFER=$BUFFER
	fi
fi

# prep fds
local -a pipes=($(mktemp -u){1,2})
mkfifo $pipes || return 3
exec {_ZPLUMBER_OUT_FD}<> $pipes[1]
exec {_ZPLUMBER_CTRL_FD}<> $pipes[2]
rm $pipes

ZPLUMBER_FILE=$(umask 066; mktemp)

# Saved for history
typeset -g _ZPLUMBER_START_BUFFER=$BUFFER

# needs to be outside of 'emulate -L zsh'
# for user settings like rcquotes, extendedglob, etc.
# to have an effect in $BUFFER
# in case of (global |)aliases, quote EVERYTHING
eval "${BUFFER%\|*} > >(
	'command' 'cat' >| '$_ZPLUMBER_FILE' & 'builtin' 'typeset' 'pid='\$!
	'builtin' 'read' '-k1' '-u' '$_ZPLUMBER_CTRL_FD'; 'builtin' 'kill' \$pid
	'command' 'cat' >&$_ZPLUMBER_OUT_FD
) & _ZPLUMBER_START_PID=\$!"
add-zle-hook-widget line-init →plumber-line-init
BUFFER=
# TODO: history

# vim: syntax=zsh
